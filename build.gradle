plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

base.archivesName = project.archives_base_name
version = "$project.version+${libs.versions.minecraft.get()}"
group = project.maven_group

repositories {
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		url = "https://jitpack.io"
	}
}

dependencies {
	minecraft libs.minecraft
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.21:2024.07.07@zip")
	}

	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.5")))
}

processResources {
	inputs.property 'version', version

	var identifier = project.properties['archives_base_name'].toString()
	filesMatching('quilt.mod.json') {
		expand 'id'     : identifier,
				'Id'     : identifier.split("_").toList().stream().map((str) -> str[0].toUpperCase() + str.substring(1).toLowerCase()).toArray().join(""),
				'version': version,
				'group'  : project.properties['maven_group']
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
